import {
  core.memory {load-int},
  core.text {_get-content-pointer},
  this.context.foreign {CURL_GLOBAL_DEFAULT},
  this.context.rin {_wrap-curl-code, rin},
  this.entity.error {CURL-Code, Init-Error},
}

foreign {
  curl_global_init(pointer): int,
  curl_global_cleanup(): void,
  curl_easy_init(): int,
  curl_easy_cleanup(pointer): void,
  curl_easy_setopt(pointer, int): int,
  curl_easy_getinfo(pointer, int): int,
  curl_easy_perform(pointer): int,
  curl_slist_append(pointer, pointer): pointer,
  curl_slist_free_all(pointer): void,
}

data _handle {}

define _curl_global_init(flags: int): int {
  magic external curl_global_init(flags)
}

define _curl_easy_init(): int {
  magic external curl_easy_init()
}

define _global-init(): rin(unit) {
  let code = _curl_global_init(CURL_GLOBAL_DEFAULT) in
  _wrap-curl-code(code, Unit)
}

define _global_cleanup(): unit {
  magic external curl_global_cleanup()
}

define _init(): rin(_handle) {
  let result = _curl_easy_init() in
  if eq-int(result, 0) {
    Left(Init-Error)
  } else {
    Right(magic cast(int, _handle, result))
  }
}

define _cleanup(h: _handle): unit {
  magic external curl_easy_cleanup(h)
}

define _curl_easy_getinfo(h: _handle, option: int): rin(int) {
  let buffer: int = magic alloca(int, 1) in
  let result = magic external curl_easy_getinfo(h, option)(buffer: int) in
  if eq-int(result, 0) {
    Right(load-int(buffer))
  } else {
    Left(CURL-Code(result))
  }
}

define _curl_easy_setopt(h: _handle, option: int, parameter: int): rin(unit) {
  let code = magic external curl_easy_setopt(h, option)(parameter: int) in
  _wrap-curl-code(code, Unit)
}

define _bool-to-int(b: bool): int {
  if b {
    1
  } else {
    0
  }
}

define _curl_easy_setopt_bool(h: _handle, option: int, parameter: bool): rin(unit) {
  let content = _bool-to-int(parameter) in
  let code = magic external curl_easy_setopt(h, option)(content: int) in
  _wrap-curl-code(code, Unit)
}

define _curl_easy_setopt_text(h: _handle, option: int, parameter: &text): rin(unit) {
  let content = _get-content-pointer(parameter) in
  let code = magic external curl_easy_setopt(h, option)(content: int) in
  _wrap-curl-code(code, Unit)
}

define _curl_easy_perform(h: _handle): rin(unit) {
  let code = magic external curl_easy_perform(h) in
  _wrap-curl-code(code, Unit)
}

inline _with-handle<a>(callback: (_handle) -> rin(a)): rin(a) {
  try _ = _global-init() in
  try h = _init() in
  try result =
    let tmp = callback(h) in
    _cleanup(h);
    _global_cleanup();
    tmp
  in
  Right(result)
}
