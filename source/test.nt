import {
  core.int.show {show-int},
  http-base.field {Field},
  http-base.header.show {show-header},
  http-base.request {Request},
  http-base.request-method {GET},
  http-base.response {Response},
  this.entity.config {Config},
  this.entity.error {show-error},
  this.scene.perform {perform},
}

define main(): unit {
  let result =
    perform(
      Request of {
        method = GET,
        path = *"/",
        fields = {
          [
            Field(*"My-Field", *"hoge"),
            Field(*"Content-Type", *"application/json"),
            Field(*"x-my-extension", *"Bearer 1234567890"),
          ]
        },
        body = *"こんにちは〜",
      },
      Config of {
        host = "http://localhost:8080",
        buffer-size = 1024,
        follow-location = True,
      },
    )
  in
  match result {
  | Right(response) =>
    let Response of {status-code, body, fields} = response in
    printf("success:\n{}\n", [body]);
    printf("header:\n{}\n", [show-header(fields)]);
    printf("status-code:\n{}\n", [show-int(status-code)])
  | Left(e) =>
    printf("failure: {}\n", [show-error(e)])
  }
}

define zen(): unit {
  main()
}
